plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'java'
}

version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.34.2"
    implementation(
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
            'no.tornado:tornadofx:1.7.17',
            'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2',
            'com.squareup.okhttp3:okhttp:4.2.1'

    )

    testImplementation(
            'org.assertj:assertj-core:3.12.2',
            'org.junit.jupiter:junit-jupiter-api:5.4.2',
            'com.squareup.okhttp3:mockwebserver:4.2.1'
    )
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.4.2')
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}